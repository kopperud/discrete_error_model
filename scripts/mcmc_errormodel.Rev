tree <- args[1]
beta <- args[2]

morpho <- readDiscreteCharacterData("data/tree" + tree + "_beta" + beta + ".nex")

taxa <- morpho.names()
num_taxa <- morpho.size()
n_branches <- 2 * num_taxa - 2

moves = VectorMoves()
monitors = VectorMonitors()


br_len_lambda ~ dnExp(0.2)
moves.append( mvScale(br_len_lambda, weight=2) )

phylogeny ~ dnUniformTopologyBranchLength(taxa, branchLengthDistribution=dnExponential(br_len_lambda))
moves.append( mvNNI(phylogeny, weight=n_branches/2.0) )
moves.append( mvSPR(phylogeny, weight=n_branches/10.0) )
moves.append( mvBranchLengthScale(phylogeny, weight=n_branches) )

tree_length := phylogeny.treeLength()


Q_morpho <- fnJC(2)

alpha_morpho ~ dnUniform( 0, 1E6 )
rates_morpho := fnDiscretizeGamma( alpha_morpho, alpha_morpho, 4 )

#Moves on the parameters to the Gamma distribution.
moves.append( mvScale(alpha_morpho, lambda=1, weight=2.0) )

#observation_error ~ dnBeta(1, 50)
observation_error ~ dnUniform(0.0, 0.50)
observation_error_freq <- [0.5, 0.5]
moves.append( mvScale(observation_error, lambda = 1, weight = 2.0) ) ## move on beta
phyMorpho ~ dnPhyloCTMC(tree=phylogeny, siteRates=rates_morpho, Q=Q_morpho, observationErrorProbability = observation_error, observationErrorFrequencies = observation_error_freq, type="Standard")
phyMorpho.clamp(morpho)

mymodel = model(phylogeny)


out_name <- "output/tree" + tree + "_beta" + beta + "_witherror.log"
monitors.append( mnModel(filename = out_name, printgen=10) )
tree_name <- "output/tree" + tree + "_beta" + beta + "_witherror.trees"
monitors.append( mnFile(filename = tree_name, printgen=10, phylogeny) )
monitors.append( mnScreen(printgen=100))

mymcmc = mcmc(mymodel, monitors, moves, nruns=2, combine="mixed")

mymcmc.run(generations=20000, tuningInterval=200)

q()
